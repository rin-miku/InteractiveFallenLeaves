#include "CommonUtility.cginc"

#pragma kernel AddVelocity

RWStructuredBuffer<float4> AddedVelocityBuffer; 

float2 CurrentPosition;
float2 PreviousPosition;
float VelocityPressed;
float VelocityEffectRadius;
float VelocityFalloff;
float VelocityMultiplier;

float PlayerVerticalVelocity;
float JumpEffectRadius;
float JumpVelocityMultiplier;

float DrawDirectionalLineMask(float2 origin, float2 direction, float maxLength, float2 uv, float radius, float feather)
{
    float2 delta = uv - origin;
    float projection = dot(direction, delta);
    float2 nearestPoint = direction * projection;
    float distToLine = distance(nearestPoint, delta);
    
    float isBehind = 1.0 - step(0.0, projection);
    float isBeyondLength = smoothstep(maxLength, maxLength + feather * 0.5, projection);
    float lineMask = smoothstep(radius, radius + feather, distToLine);
    
    return 1.0 - saturate(lineMask + isBehind + isBeyondLength);
}

float DrawRadialMask(float2 center, float2 uv, float radius, float feather)
{
    float dist = distance(center, uv);
    return 1.0 - smoothstep(radius, radius + feather, dist);
}

[numthreads(8, 8, 1)]
void AddVelocity (uint3 id : SV_DispatchThreadID)
{
    int2 coord = id.xy;
    
    float2 mouseMove = CurrentPosition - PreviousPosition;
    float moveLength = length(mouseMove);
    
    float2 moveVelocity = float2(0.0, 0.0);
    if (moveLength > 1e-5)
    {
        float factor = DrawDirectionalLineMask(CurrentPosition, normalize(mouseMove), moveLength, coord, VelocityEffectRadius, VelocityFalloff);
        moveVelocity = factor * mouseMove * VelocityMultiplier;
        moveVelocity *= VelocityPressed;
    }
    
    float2 jumpVelocity = float2(0.0, 0.0);
    if (abs(PlayerVerticalVelocity) > 1e-5)
    {
        float radialFactor = DrawRadialMask(CurrentPosition, coord, JumpEffectRadius, VelocityFalloff);
        float2 delta = coord - CurrentPosition;
        float len = length(delta);
        float2 dir = (len > 1e-5) ? delta / len : float2(0.0, 0.0);
        jumpVelocity = -dir * abs(PlayerVerticalVelocity) * JumpVelocityMultiplier * radialFactor;
    }

    AddedVelocityBuffer[CoordToIndex(coord)] += float4(moveVelocity + jumpVelocity, 0.0, 0.0);
}


#pragma kernel AddDensity

RWStructuredBuffer<float4> AddedDensityBuffer;

float4 DensityColor;
float2 MousePosition;
float DensityPressed;
float DensityEffectRadius;
float DensityFalloff;

[numthreads(8, 8, 1)]
void AddDensity(uint3 id : SV_DispatchThreadID)
{
    int2 coord = id.xy;
    float2 temp = (float) id.xy / 512.0;
    
    float2 vectorToMouse = MousePosition - (float2)coord;
    float distance = length(vectorToMouse);
    
    if (distance > DensityEffectRadius)
        return;
    
    float densityAmout = 1 - smoothstep(DensityEffectRadius - DensityFalloff, DensityEffectRadius, distance);
    
    densityAmout *= DensityPressed;
    
    float4 oldValue = AddedDensityBuffer[CoordToIndex(coord)];
    float3 newValue = lerp(oldValue.rgb, DensityColor.rgb, densityAmout);

    AddedDensityBuffer[CoordToIndex(coord)] = float4(newValue, 1);
}